SET SERVEROUTPUT ON;
SET VERIFY OFF;

DROP TABLE USUARIO_TAREFA CASCADE CONSTRAINTS;
DROP TABLE HISTORICO_MOOD CASCADE CONSTRAINTS;
DROP TABLE TAREFA CASCADE CONSTRAINTS;
DROP TABLE USUARIO CASCADE CONSTRAINTS;
DROP TABLE AUDITORIA CASCADE CONSTRAINTS;
DROP TABLE LOG_ERROS CASCADE CONSTRAINTS;
DROP TABLE COMPETENCIA CASCADE CONSTRAINTS;
DROP TABLE USUARIO_COMPETENCIA CASCADE CONSTRAINTS;
DROP TABLE TAREFA_COMPETENCIA CASCADE CONSTRAINTS;


CREATE TABLE LOG_ERROS (
  ID_LOG NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  PROCEDIMENTO VARCHAR2(200),
  MENSAGEM VARCHAR2(4000),
  DATA_EVENTO DATE DEFAULT SYSDATE
);

CREATE TABLE USUARIO (
  ID_USUARIO NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  NOME VARCHAR2(100) NOT NULL,
  EMAIL VARCHAR2(150),
  DATA_INSCRICAO DATE DEFAULT SYSDATE,
  ULTIMA_ATUALIZACAO DATE
);

CREATE TABLE TAREFA (
  ID_TAREFA NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  NOME_TAREFA VARCHAR2(150) NOT NULL,
  DESCRICAO VARCHAR2(400),
  ID_USUARIO_CRIADOR NUMBER,
  CONSTRAINT FK_TAREFA_USUARIO FOREIGN KEY (ID_USUARIO_CRIADOR)
      REFERENCES USUARIO(ID_USUARIO)
);

CREATE TABLE USUARIO_TAREFA (
  ID_USUARIO_TAREFA NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  ID_USUARIO NUMBER NOT NULL,
  ID_TAREFA NUMBER NOT NULL,
  CONQUISTA CHAR(1) DEFAULT '0',
  DATA_INICIO DATE DEFAULT SYSDATE,
  DATA_FIM DATE,
  PROGRESSO_PERCENTUAL NUMBER(3) DEFAULT 0,
  CONSTRAINT FK_UT_USUARIO FOREIGN KEY (ID_USUARIO) REFERENCES USUARIO(ID_USUARIO),
  CONSTRAINT FK_UT_TAREFA FOREIGN KEY (ID_TAREFA) REFERENCES TAREFA(ID_TAREFA)
);

CREATE TABLE HISTORICO_MOOD (
  ID_HISTORICO NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  ID_USUARIO NUMBER NOT NULL,
  HUMOR NUMBER(3),
  DATA_REGISTRO DATE DEFAULT SYSDATE,
  OBSERVACAO VARCHAR2(400),
  CONSTRAINT FK_HM_USUARIO FOREIGN KEY (ID_USUARIO) REFERENCES USUARIO(ID_USUARIO)
);

CREATE TABLE AUDITORIA (
  ID_AUDITORIA NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  ACAO VARCHAR2(50),
  TABELA_AFETADA VARCHAR2(50),
  ID_REGISTRO NUMBER,
  DATA_EVENTO DATE DEFAULT SYSDATE,
  ID_USUARIO NUMBER
);

CREATE TABLE COMPETENCIA (
  ID_COMPETENCIA NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  NOME_COMPETENCIA VARCHAR2(150) NOT NULL
);

CREATE TABLE USUARIO_COMPETENCIA (
  ID_US_COMP NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  ID_USUARIO NUMBER NOT NULL,
  ID_COMPETENCIA NUMBER NOT NULL,
  NIVEL NUMBER(2) DEFAULT 1,
  CONSTRAINT FK_UC_USUARIO FOREIGN KEY (ID_USUARIO) REFERENCES USUARIO(ID_USUARIO),
  CONSTRAINT FK_UC_COMP FOREIGN KEY (ID_COMPETENCIA) REFERENCES COMPETENCIA(ID_COMPETENCIA)
);

CREATE TABLE TAREFA_COMPETENCIA (
  ID_TC NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  ID_TAREFA NUMBER NOT NULL,
  ID_COMPETENCIA NUMBER NOT NULL,
  NIVEL_EXIGIDO NUMBER(2) DEFAULT 1,
  CONSTRAINT FK_TC_TAREFA FOREIGN KEY (ID_TAREFA) REFERENCES TAREFA(ID_TAREFA),
  CONSTRAINT FK_TC_COMP FOREIGN KEY (ID_COMPETENCIA) REFERENCES COMPETENCIA(ID_COMPETENCIA)
);

CREATE OR REPLACE PROCEDURE AUDIT_INSERT_UPDATE_DELETE(
  p_acao IN VARCHAR2,
  p_tabela IN VARCHAR2,
  p_id     IN NUMBER,
  p_usuario IN NUMBER
) AS
BEGIN
  INSERT INTO AUDITORIA(ACAO, TABELA_AFETADA, ID_REGISTRO, ID_USUARIO)
  VALUES(p_acao, p_tabela, p_id, p_usuario);
EXCEPTION WHEN OTHERS THEN
  NULL;
END;
/

CREATE OR REPLACE TRIGGER TRG_AUD_USUARIO
AFTER INSERT OR UPDATE OR DELETE ON USUARIO
FOR EACH ROW
BEGIN
  IF INSERTING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('INSERT','USUARIO', :NEW.ID_USUARIO, :NEW.ID_USUARIO);
  ELSIF UPDATING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('UPDATE','USUARIO', :NEW.ID_USUARIO, :NEW.ID_USUARIO);
  ELSIF DELETING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('DELETE','USUARIO', :OLD.ID_USUARIO, :OLD.ID_USUARIO);
  END IF;
END;
/

CREATE OR REPLACE TRIGGER TRG_AUD_TAREFA
AFTER INSERT OR UPDATE OR DELETE ON TAREFA
FOR EACH ROW
BEGIN
  IF INSERTING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('INSERT','TAREFA', :NEW.ID_TAREFA, :NEW.ID_USUARIO_CRIADOR);
  ELSIF UPDATING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('UPDATE','TAREFA', :NEW.ID_TAREFA, :NEW.ID_USUARIO_CRIADOR);
  ELSIF DELETING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('DELETE','TAREFA', :OLD.ID_TAREFA, :OLD.ID_USUARIO_CRIADOR);
  END IF;
END;
/

CREATE OR REPLACE TRIGGER TRG_AUD_USUARIO_TAREFA
AFTER INSERT OR UPDATE OR DELETE ON USUARIO_TAREFA
FOR EACH ROW
BEGIN
  IF INSERTING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('INSERT','USUARIO_TAREFA', :NEW.ID_USUARIO_TAREFA, :NEW.ID_USUARIO);
  ELSIF UPDATING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('UPDATE','USUARIO_TAREFA', :NEW.ID_USUARIO_TAREFA, :NEW.ID_USUARIO);
  ELSIF DELETING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('DELETE','USUARIO_TAREFA', :OLD.ID_USUARIO_TAREFA, :OLD.ID_USUARIO);
  END IF;
END;
/

CREATE OR REPLACE TRIGGER TRG_AUD_HISTORICO_MOOD
AFTER INSERT OR UPDATE OR DELETE ON HISTORICO_MOOD
FOR EACH ROW
BEGIN
  IF INSERTING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('INSERT','HISTORICO_MOOD', :NEW.ID_HISTORICO, :NEW.ID_USUARIO);
  ELSIF UPDATING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('UPDATE','HISTORICO_MOOD', :NEW.ID_HISTORICO, :NEW.ID_USUARIO);
  ELSIF DELETING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('DELETE','HISTORICO_MOOD', :OLD.ID_HISTORICO, :OLD.ID_USUARIO);
  END IF;
END;
/

CREATE OR REPLACE TRIGGER TRG_AUD_COMPETENCIA
AFTER INSERT OR UPDATE OR DELETE ON COMPETENCIA
FOR EACH ROW
BEGIN
  IF INSERTING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('INSERT','COMPETENCIA', :NEW.ID_COMPETENCIA, NULL);
  ELSIF UPDATING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('UPDATE','COMPETENCIA', :NEW.ID_COMPETENCIA, NULL);
  ELSIF DELETING THEN
    MindCare_PKG.AUDIT_INSERT_UPDATE_DELETE('DELETE','COMPETENCIA', :OLD.ID_COMPETENCIA, NULL);
  END IF;
END;
/



CREATE OR REPLACE PACKAGE MindCare_PKG AS
  PROCEDURE AUDIT_INSERT_UPDATE_DELETE(
    p_acao    IN VARCHAR2,
    p_tabela  IN VARCHAR2,
    p_id      IN NUMBER,
    p_usuario IN NUMBER
  );

  PROCEDURE CriarUsuario(p_nome VARCHAR2, p_email VARCHAR2);
  PROCEDURE RegistrarHumor(p_id_usuario NUMBER, p_humor NUMBER, p_obs VARCHAR2);
  PROCEDURE CriarTarefa(p_nome VARCHAR2, p_desc VARCHAR2, p_criador NUMBER);

  FUNCTION ListarUsuarios RETURN CLOB;
  FUNCTION AnalisarCompatibilidade(p_id_usuario NUMBER, p_id_tarefa NUMBER) RETURN CLOB;

  PROCEDURE ExportarDatasetJSON(p_out OUT CLOB);
  PROCEDURE PopularDadosDeTeste;
END MindCare_PKG;
/


CREATE OR REPLACE PACKAGE BODY MindCare_PKG AS

  PROCEDURE AUDIT_INSERT_UPDATE_DELETE(
    p_acao    IN VARCHAR2,
    p_tabela  IN VARCHAR2,
    p_id      IN NUMBER,
    p_usuario IN NUMBER
  ) IS
  BEGIN
    INSERT INTO AUDITORIA(ACAO, TABELA_AFETADA, ID_REGISTRO, ID_USUARIO)
    VALUES(p_acao, p_tabela, p_id, p_usuario);
  EXCEPTION
    WHEN OTHERS THEN
      NULL; 
  END AUDIT_INSERT_UPDATE_DELETE;

  PROCEDURE CriarUsuario(p_nome VARCHAR2, p_email VARCHAR2) IS
    v_erro VARCHAR2(4000);
  BEGIN
    INSERT INTO USUARIO (NOME, EMAIL, ULTIMA_ATUALIZACAO)
      VALUES (p_nome, p_email, SYSDATE);
  EXCEPTION
    WHEN OTHERS THEN
      v_erro := SQLERRM;
      INSERT INTO LOG_ERROS (PROCEDIMENTO, MENSAGEM) VALUES ('CriarUsuario', v_erro);
      RAISE;
  END CriarUsuario;


  PROCEDURE RegistrarHumor(p_id_usuario NUMBER, p_humor NUMBER, p_obs VARCHAR2) IS
    vCount NUMBER;
    v_erro VARCHAR2(4000);
  BEGIN
    SELECT COUNT(*) INTO vCount FROM USUARIO WHERE ID_USUARIO = p_id_usuario;

    IF vCount = 0 THEN
      RAISE_APPLICATION_ERROR(-20001,'Usuario nao encontrado');
    END IF;

    IF p_humor < 0 OR p_humor > 10 THEN
      RAISE_APPLICATION_ERROR(-20002,'Valor de humor fora do intervalo (0-10)');
    END IF;

    INSERT INTO HISTORICO_MOOD (ID_USUARIO, HUMOR, OBSERVACAO)
      VALUES (p_id_usuario, p_humor, p_obs);
  EXCEPTION
    WHEN OTHERS THEN
      v_erro := SQLERRM;
      INSERT INTO LOG_ERROS (PROCEDIMENTO, MENSAGEM) VALUES ('RegistrarHumor', v_erro);
      RAISE;
  END RegistrarHumor;


  PROCEDURE CriarTarefa(p_nome VARCHAR2, p_desc VARCHAR2, p_criador NUMBER) IS
    v_erro VARCHAR2(4000);
  BEGIN
    INSERT INTO TAREFA (NOME_TAREFA, DESCRICAO, ID_USUARIO_CRIADOR)
      VALUES (p_nome, p_desc, p_criador);
  EXCEPTION
    WHEN OTHERS THEN
      v_erro := SQLERRM;
      INSERT INTO LOG_ERROS (PROCEDIMENTO, MENSAGEM) VALUES ('CriarTarefa', v_erro);
      RAISE;
  END CriarTarefa;


  FUNCTION ListarUsuarios RETURN CLOB IS
    vJSON CLOB;
    vFirst BOOLEAN := TRUE;
    v_erro VARCHAR2(4000);
    CURSOR c IS
      SELECT ID_USUARIO,
             REPLACE(NOME, '"', CHR(92)||CHR(34)) AS NOME_ESCAPADO,
             REPLACE(EMAIL, '"', CHR(92)||CHR(34)) AS EMAIL_ESCAPADO
        FROM USUARIO
       ORDER BY ID_USUARIO;
  BEGIN
    DBMS_LOB.CREATETEMPORARY(vJSON, TRUE);
    DBMS_LOB.APPEND(vJSON, '[');

    FOR r IN c LOOP
      IF NOT vFirst THEN
        DBMS_LOB.APPEND(vJSON, ',');
      END IF;
      vFirst := FALSE;

      DBMS_LOB.APPEND(vJSON,
        '{"id_usuario":' || TO_CHAR(r.ID_USUARIO) ||
        ',"nome":"'   || r.NOME_ESCAPADO ||
        '","email":"' || NVL(r.EMAIL_ESCAPADO,'') || '"}');
    END LOOP;

    DBMS_LOB.APPEND(vJSON, ']');
    RETURN vJSON;
  EXCEPTION
    WHEN OTHERS THEN
      v_erro := SQLERRM;
      BEGIN
        IF DBMS_LOB.ISTEMPORARY(vJSON) = 1 THEN
          DBMS_LOB.FREETEMPORARY(vJSON);
        END IF;
      EXCEPTION WHEN OTHERS THEN NULL; END;
      INSERT INTO LOG_ERROS (PROCEDIMENTO, MENSAGEM) VALUES ('ListarUsuarios', v_erro);
      RAISE;
  END ListarUsuarios;


  FUNCTION AnalisarCompatibilidade(p_id_usuario NUMBER, p_id_tarefa NUMBER) RETURN CLOB IS
    v_json CLOB;
    v_erro VARCHAR2(4000);
    v_email USUARIO.EMAIL%TYPE;
    v_user_count NUMBER;
    v_task_count NUMBER;
    v_total_required NUMBER := 0;
    v_matched NUMBER := 0;

    CURSOR c_task_comp IS
      SELECT tc.ID_COMPETENCIA, c.NOME_COMPETENCIA, tc.NIVEL_EXIGIDO
        FROM TAREFA_COMPETENCIA tc
        JOIN COMPETENCIA c ON c.ID_COMPETENCIA = tc.ID_COMPETENCIA
       WHERE tc.ID_TAREFA = p_id_tarefa;
  BEGIN
    DBMS_LOB.CREATETEMPORARY(v_json, TRUE);

    SELECT COUNT(*) INTO v_user_count FROM USUARIO WHERE ID_USUARIO = p_id_usuario;
    SELECT COUNT(*) INTO v_task_count FROM TAREFA WHERE ID_TAREFA = p_id_tarefa;

    IF v_user_count = 0 OR v_task_count = 0 THEN
      RAISE_APPLICATION_ERROR(-20010,'Usuario ou Tarefa nao encontrados');
    END IF;

    SELECT EMAIL INTO v_email FROM USUARIO WHERE ID_USUARIO = p_id_usuario;

    IF v_email IS NULL THEN
      DBMS_LOB.APPEND(v_json,'{"erro":"email_nao_informado"}');
      RETURN v_json;
    END IF;

    IF NOT REGEXP_LIKE(v_email, '^[A-Za-z0-9._%+-]+@[A-Za-z0-9.-]+\.[A-Za-z]{2,}$') THEN
      DBMS_LOB.APPEND(v_json,'{"erro":"email_formato_invalido"}');
      RETURN v_json;
    END IF;

    FOR rc IN c_task_comp LOOP
      v_total_required := v_total_required + 1;
      DECLARE
        v_user_level NUMBER := 0;
      BEGIN
        SELECT NVL(MAX(NIVEL),0) INTO v_user_level
          FROM USUARIO_COMPETENCIA
         WHERE ID_USUARIO = p_id_usuario
           AND ID_COMPETENCIA = rc.ID_COMPETENCIA;

        IF v_user_level >= rc.NIVEL_EXIGIDO THEN
          v_matched := v_matched + 1;
        END IF;
      EXCEPTION WHEN NO_DATA_FOUND THEN
        NULL;
      END;
    END LOOP;

    DECLARE
      v_compat NUMBER := 0;
      v_msg VARCHAR2(200);
    BEGIN
      IF v_total_required = 0 THEN
        v_compat := 0;
        v_msg := 'nenhuma_competencia_exigida';
      ELSE
        v_compat := ROUND((v_matched / v_total_required) * 100);
        v_msg := 'ok';
      END IF;

      DBMS_LOB.APPEND(v_json,
        '{"id_usuario":' || TO_CHAR(p_id_usuario) ||
        ',"id_tarefa":' || TO_CHAR(p_id_tarefa) ||
        ',"total_exigido":'|| TO_CHAR(v_total_required) ||
        ',"matched":'|| TO_CHAR(v_matched) ||
        ',"compatibilidade":'|| TO_CHAR(v_compat) ||
        ',"mensagem":"' || v_msg || '"}');
      RETURN v_json;
    END;
  EXCEPTION
    WHEN OTHERS THEN
      v_erro := SQLERRM;
      BEGIN
        IF DBMS_LOB.ISTEMPORARY(v_json) = 1 THEN
          DBMS_LOB.FREETEMPORARY(v_json);
        END IF;
      EXCEPTION WHEN OTHERS THEN NULL; END;
      INSERT INTO LOG_ERROS (PROCEDIMENTO, MENSAGEM) VALUES ('AnalisarCompatibilidade', v_erro);
      RAISE;
  END AnalisarCompatibilidade;

  PROCEDURE ExportarDatasetJSON(p_out OUT CLOB) IS
    vJSON CLOB;
    v_part CLOB;
    v_first BOOLEAN;
    v_erro VARCHAR2(4000);
    CURSOR c_u IS SELECT * FROM USUARIO ORDER BY ID_USUARIO;
    CURSOR c_t IS SELECT * FROM TAREFA ORDER BY ID_TAREFA;
    CURSOR c_m IS SELECT * FROM HISTORICO_MOOD ORDER BY ID_HISTORICO;
  BEGIN
    DBMS_LOB.CREATETEMPORARY(vJSON, TRUE);
    DBMS_LOB.APPEND(vJSON,'{');

    -- usuarios
    DBMS_LOB.CREATETEMPORARY(v_part, TRUE);
    DBMS_LOB.APPEND(v_part,'[');
    v_first := TRUE;
    FOR r IN c_u LOOP
      IF NOT v_first THEN DBMS_LOB.APPEND(v_part,','); END IF;
      v_first := FALSE;
      DBMS_LOB.APPEND(v_part,'{"id":'||r.ID_USUARIO||',"nome":"'||REPLACE(r.NOME, '"', CHR(92)||CHR(34))||'","email":"'||NVL(r.EMAIL,'')||'"}');
    END LOOP;
    DBMS_LOB.APPEND(v_part,']');
    DBMS_LOB.APPEND(vJSON,'"usuarios":'||v_part||',');

    DBMS_LOB.CREATETEMPORARY(v_part, TRUE);
    DBMS_LOB.APPEND(v_part,'[');
    v_first := TRUE;
    FOR r IN c_t LOOP
      IF NOT v_first THEN DBMS_LOB.APPEND(v_part,','); END IF;
      v_first := FALSE;
      DBMS_LOB.APPEND(v_part,
        '{"id":'||r.ID_TAREFA||
        ',"nome":"'||REPLACE(r.NOME_TAREFA, '"', CHR(92)||CHR(34))||
        '","criador":'||CASE WHEN r.ID_USUARIO_CRIADOR IS NULL THEN 'null' ELSE TO_CHAR(r.ID_USUARIO_CRIADOR) END||'}');
    END LOOP;
    DBMS_LOB.APPEND(v_part,']');
    DBMS_LOB.APPEND(vJSON,'"tarefas":'||v_part||',');

    DBMS_LOB.CREATETEMPORARY(v_part, TRUE);
    DBMS_LOB.APPEND(v_part,'[');
    v_first := TRUE;
    FOR r IN c_m LOOP
      IF NOT v_first THEN DBMS_LOB.APPEND(v_part,','); END IF;
      v_first := FALSE;
      DBMS_LOB.APPEND(v_part,
        '{"id":'||r.ID_HISTORICO||
        ',"usuario":'||r.ID_USUARIO||
        ',"humor":'||NVL(TO_CHAR(r.HUMOR),'null')||
        ',"obs":"'||REPLACE(NVL(r.OBSERVACAO,''), '"', CHR(92)||CHR(34))||'"}');
    END LOOP;
    DBMS_LOB.APPEND(v_part,']');
    DBMS_LOB.APPEND(vJSON,'"moods":'||v_part);

    DBMS_LOB.APPEND(vJSON,'}');
    p_out := vJSON;

    BEGIN
      IF DBMS_LOB.ISTEMPORARY(v_part) = 1 THEN
        DBMS_LOB.FREETEMPORARY(v_part);
      END IF;
    EXCEPTION WHEN OTHERS THEN NULL; END;

  EXCEPTION
    WHEN OTHERS THEN
      v_erro := SQLERRM;
      BEGIN
        IF DBMS_LOB.ISTEMPORARY(vJSON) = 1 THEN
          DBMS_LOB.FREETEMPORARY(vJSON);
        END IF;
        IF DBMS_LOB.ISTEMPORARY(v_part) = 1 THEN
          DBMS_LOB.FREETEMPORARY(v_part);
        END IF;
      EXCEPTION WHEN OTHERS THEN NULL; END;
      INSERT INTO LOG_ERROS (PROCEDIMENTO, MENSAGEM) VALUES ('ExportarDatasetJSON', v_erro);
      RAISE;
  END ExportarDatasetJSON;

  PROCEDURE PopularDadosDeTeste IS
  v_email VARCHAR2(200);
  v_erro  VARCHAR2(4000);
  v_user_id USUARIO.ID_USUARIO%TYPE;
  v_task_id TAREFA.ID_TAREFA%TYPE;
  v_comp_id COMPETENCIA.ID_COMPETENCIA%TYPE;
  TYPE t_user_ids IS TABLE OF USUARIO.ID_USUARIO%TYPE;
  l_user_ids t_user_ids := t_user_ids();
  TYPE t_task_ids IS TABLE OF TAREFA.ID_TAREFA%TYPE;
  l_task_ids t_task_ids := t_task_ids();
  TYPE t_comp_ids IS TABLE OF COMPETENCIA.ID_COMPETENCIA%TYPE;
  l_comp_ids t_comp_ids := t_comp_ids();
BEGIN

  FOR i IN 1..10 LOOP
    v_email := 'user'||i||'@example.com';
    INSERT INTO USUARIO (NOME, EMAIL, ULTIMA_ATUALIZACAO)
      VALUES ('Usuario Teste '||i, v_email, SYSDATE)
      RETURNING ID_USUARIO INTO v_user_id;
    l_user_ids.EXTEND;
    l_user_ids(l_user_ids.COUNT) := v_user_id;
  END LOOP;


  FOR c IN (SELECT COLUMN_VALUE AS nome FROM TABLE(SYS.ODCIVARCHAR2LIST(
                 'Comunicação','Programação','Gerenciamento de Projetos','Design','Análise de Dados'))) LOOP
    INSERT INTO COMPETENCIA(NOME_COMPETENCIA) VALUES(c.nome)
      RETURNING ID_COMPETENCIA INTO v_comp_id;
    l_comp_ids.EXTEND;
    l_comp_ids(l_comp_ids.COUNT) := v_comp_id;
  END LOOP;

  FOR t IN 1..10 LOOP
    INSERT INTO TAREFA(NOME_TAREFA, DESCRICAO, ID_USUARIO_CRIADOR)
      VALUES('Tarefa Teste '||t, 'Descricao da tarefa '||t, l_user_ids(1))
      RETURNING ID_TAREFA INTO v_task_id;
    l_task_ids.EXTEND;
    l_task_ids(l_task_ids.COUNT) := v_task_id;
  END LOOP;


  FOR i IN 1..l_user_ids.COUNT LOOP
    INSERT INTO USUARIO_COMPETENCIA(ID_USUARIO, ID_COMPETENCIA, NIVEL)
      VALUES(l_user_ids(i), l_comp_ids(MOD(i,5)+1), MOD(i,3)+1);
    INSERT INTO USUARIO_COMPETENCIA(ID_USUARIO, ID_COMPETENCIA, NIVEL)
      VALUES(l_user_ids(i), l_comp_ids(MOD(i+1,5)+1), MOD(i+2,3)+1);
  END LOOP;


  FOR i IN 1..l_task_ids.COUNT LOOP
    INSERT INTO TAREFA_COMPETENCIA(ID_TAREFA, ID_COMPETENCIA, NIVEL_EXIGIDO)
      VALUES(l_task_ids(i), l_comp_ids(MOD(i,5)+1), 1);
    INSERT INTO TAREFA_COMPETENCIA(ID_TAREFA, ID_COMPETENCIA, NIVEL_EXIGIDO)
      VALUES(l_task_ids(i), l_comp_ids(MOD(i+2,5)+1), 2);
  END LOOP;


  FOR i IN 1..l_user_ids.COUNT LOOP
    INSERT INTO HISTORICO_MOOD(ID_USUARIO, HUMOR, OBSERVACAO)
      VALUES(l_user_ids(i), MOD(i,11), 'Mood sample '||i);
  END LOOP;


  FOR i IN 1..LEAST(l_user_ids.COUNT, l_task_ids.COUNT) LOOP
    INSERT INTO USUARIO_TAREFA(ID_USUARIO, ID_TAREFA, CONQUISTA, PROGRESSO_PERCENTUAL)
      VALUES(l_user_ids(i), l_task_ids(i),
             CASE WHEN MOD(i,3)=0 THEN '1' ELSE '0' END,
             MOD(i*7,100));
  END LOOP;

  COMMIT;

EXCEPTION
  WHEN OTHERS THEN
    v_erro := SQLERRM;
    INSERT INTO LOG_ERROS (PROCEDIMENTO, MENSAGEM) VALUES ('PopularDadosDeTeste', v_erro);
    RAISE;
END PopularDadosDeTeste;

END MindCare_PKG;
/



BEGIN
  MindCare_PKG.PopularDadosDeTeste;
  DBMS_OUTPUT.PUT_LINE('PopularDadosDeTeste OK');
END;
/

DECLARE 
  v_res CLOB;
BEGIN
  v_res := MindCare_PKG.AnalisarCompatibilidade(51,14);  
  DBMS_OUTPUT.PUT_LINE(v_res);
END;
/

DECLARE
  v_dataset CLOB;
BEGIN
  MindCare_PKG.ExportarDatasetJSON(v_dataset);
  DBMS_OUTPUT.PUT_LINE('Tamanho JSON: '|| LENGTH(v_dataset));
END;
/

DECLARE
  v_json CLOB;
BEGIN
  v_json := MindCare_PKG.ListarUsuarios;
  DBMS_OUTPUT.PUT_LINE(v_json);
END;
/